# PROMPT="%n at %m in %d"

# TODO Change command status to icon

NEWLINE=$'\n'

## autoload vcs and colors
autoload -Uz vcs_info
autoload -U colors && colors

# setup a hook that runs before every ptompt.
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst

# add a function to check for untracked files in the directory.
# from https://github.com/zsh-users/zsh/blob/master/Misc/vcs_info-examples
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
#
+vi-git-untracked(){
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep '??' &> /dev/null ; then
        # This will show the marker if there are any untracked files in repo.
        # If instead you want to show the marker only if there are untracked
        # files in $PWD, use:
        #[[ -n $(git ls-files --others --exclude-standard) ]] ; then
        hook_com[staged]+='!' # signify new files with a bang
    fi
}

zstyle ':vcs_info:*' check-for-changes true
# zstyle ':vcs_info:git:*' formats " %r/%S %b %m%u%c "
zstyle ':vcs_info:git:*' formats " %{$fg[blue]%}(%{$fg[red]%}%m%u%c %{$fg[yellow]%}%{$fg[magenta]%} %b%{$fg[blue]%})"


PROMPT="┌┤ "
PROMPT+="%? "                        # Status of last command
PROMPT+="%* "                        # Timestamp
PROMPT+="%F{blue}%B%n%b%f "          # Username
PROMPT+="at " 
PROMPT+="%F{red}%B%m%b%f "           # Hostname
PROMPT+="in "
PROMPT+="%F{green}%B%1d%b%f"         # Directory
PROMPT+="\$vcs_info_msg_0_ "         # Git status
PROMPT+="${NEWLINE}"
PROMPT+="%F{white}└─╼%f "
